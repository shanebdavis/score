import &StandardImport

class PlayerEditor extends FluxComponent
  @subscriptions :navState.navState

  @propFields
    playerId: null

  @stateFields
    name:   ""
    color:  null
    confirmDeletePlayer: false

  getInitialState: ->
    if @existingPlayer
      {}
    else
      name: ""
      color: @models.currentGame.unusedColor

  @getter
    existingPlayer: -> @models.currentGame.players[@playerId]
    newPlayer: -> !@existingPlayer
    player: -> @existingPlayer ? @state
    name:   -> @player.name
    color:  -> @player.color

  deletePlayer: ->
    @models.currentGame.deletePlayer @playerId unless @newPlayer
    @close()

  setPlayerState: (state)->
    if @newPlayer
      @setState state
    else
      @models.currentGame.updatePlayer @playerId, state

  @setter
    color: (color) -> @setPlayerState {} color
    name:  (name)  -> @setPlayerState {} name

  done: ->
    if @newPlayer
      @models.currentGame.newPlayer {} @name, @color

    @close()

  close: ->
    @models.navState.clearEditPlayerId()

  render: ->
    {color} = @
    confirmDelete = !!@confirmDeletePlayer
    legalName = present @name

    Element
      childrenLayout: :column

      &ColorPicker action: @setColor, hidden: confirmDelete, selected: @color
      Element
        size: h: gridSize
      Element
        size: h: gridSize
        childrenLayout: :row
        &Button {} color, width: 2,                            hidden: confirmDelete, icon: :-, action: @toggleConfirmDeletePlayer
        &Button {}
          color
          width: 6
          delay: animationDelay * 2
          beforeText:
            confirmDelete && "delete"
          text: @name + if confirmDelete then "?" else "|"
          disabled: false
        &Button {} color, width: 2, delay: animationDelay * 6, disabled: !legalName, hidden: confirmDelete, icon: :check, action: @done

      &Keyboard {}
        color
        hidden: confirmDelete
        value: @name
        valueChanged: @setName

      if confirmDelete
        Element
          inFlow: false
          size:
            wcw:1
            h: gridSize
          location:
            x: gridSize * 2
            y: gridSize * 3
          childrenLayout: :row
          &Button {} color, width: 3, delay: animationDelay*5, icon: :*     action: @clearConfirmDeletePlayer
          &Button {} color, width: 3, delay: animationDelay*7, icon: :check action: @deletePlayer
